// For those who want the bleeding edge
buildscript {
    ext.kotlin_version = '1.2.20'
    //noinspection InconsistentLineSeparators,InconsistentLineSeparators,InconsistentLineSeparators
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}
apply plugin: 'kotlin'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.github.johnrengelman.shadow'

version = "3.2"
group = "lingerloot" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "LingeringLoot-1.12.2"

minecraft {
    version = "1.12.2-14.23.0.2493"
    runDir = "run"
    
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not allways work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "snapshot_20170926"
    makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

repositories {
    jcenter()
    maven {
        url = 'http://unascribed.com/maven/releases'
        url = 'https://repo.elytradev.com'
    }
    maven {
        url "http://maven.shadowfacts.net/"
    }
}

dependencies {
    compile group: "net.shadowfacts", name: "Forgelin", version: "1.6.0"
    deobfCompile 'com.elytradev.concrete:concrete-common:0.3.7-SNAPSHOT'
    shadow 'com.elytradev.concrete:concrete-common:0.3.7-SNAPSHOT'
    deobfCompile 'com.elytradev.concrete:concrete-network:0.3.7-SNAPSHOT'
    shadow 'com.elytradev.concrete:concrete-network:0.3.7-SNAPSHOT'
    deobfCompile 'com.elytradev.concrete:concrete-reflect:0.3.7-SNAPSHOT'
    shadow 'com.elytradev.concrete:concrete-reflect:0.3.7-SNAPSHOT'
}

shadowJar {
    relocate 'org.jetbrains.annotations', 'lingerlootkot.jetbrains.annotations'
    relocate 'com.elytradev.concrete', 'lingerlootkot.repackage.com.elytradev.concrete'
    classifier ''
    configurations = [project.configurations.shadow]
}

reobf {
    shadowJar { mappingType = 'SEARGE' }
}

tasks.build.dependsOn reobfShadowJar

artifacts {
    archives shadowJar
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

jar {
    classifier = 'slim'
    manifest {
        attributes 'FMLAT': 'lingeringloot_at.cfg'
    }
}